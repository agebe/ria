plugins {
  id "com.github.ben-manes.versions" version "0.45.0"
  id 'com.palantir.git-version' version '2.0.0'
}

version = project.file('version').text.trim()

allprojects {
  group = 'io.github.agebe'
}

subprojects {

  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'eclipse'
  apply plugin: "maven-publish"
  apply plugin: 'signing'

  version = project.file('../version').text.trim()

  repositories {
    mavenCentral()
  }

  jar {
    manifest {
      attributes(
        'Implementation-Version': project.version,
        'Implementation-Title': project.name,
        'git-version': gitVersion())
    }
  }

  // https://docs.gradle.org/current/userguide/publishing_maven.html#publishing_maven:complete_example
  publishing {
    repositories {
      maven {
        def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
        def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
        url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        credentials {
          username = project.findProperty("sonatype.user") ?: ''
          password = project.findProperty("sonatype.password") ?: ''
        }
      }
    }
    publications {
      maven(MavenPublication) {
        from components.java
        pom {
          name = 'ria'
          description = 'jvm script language'
          url = 'https://github.com/agebe/ria'
          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              id = 'agebe'
              name = 'Andre Gebers'
              email = 'andre.gebers@gmail.com'
            }
          }
          scm {
            connection = 'scm:git:git://github.com/agebe/ria.git'
            developerConnection = 'scm:git:ssh://github.com:agebe/ria.git'
            url = 'https://github.com/agebe/ria/tree/main'
          }
        }
      }
    }
  }

  signing {
    sign publishing.publications.maven
  }

  java {
    withSourcesJar()
    withJavadocJar()
  }

  javadoc {
    options.links = ['https://docs.oracle.com/en/java/javase/17/docs/api/']
  }

  test {
    useJUnitPlatform()
  }
}
